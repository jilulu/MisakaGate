apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "com.mahoucoder.misakagate"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            File signingPropFile = rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                Properties signingProp = new Properties()
                signingProp.load(signingPropFile.newDataInputStream())
                storeFile file(signingProp.get("release.storeFile"))
                storePassword signingProp.get("release.storePassord")
                keyAlias signingProp.get("release.keyAlias")
                keyPassword signingProp.get("release.keyPassword")
            } else if (System.getenv('encrypted_046cd0986819_key') != null && System.getenv('encrypted_046cd0986819_iv') != null) {
                storeFile rootProject.file('gate.jks')
                storePassword System.getenv("keyStorePassword")
                keyAlias System.getenv("keyAlias")
                keyPassword System.getenv("keyPassword")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (rootProject.file('signing.properties').exists() ||
                    (System.getenv('encrypted_046cd0986819_key') != null && System.getenv('encrypted_046cd0986819_iv') != null)) {
                signingConfig signingConfigs.release
            }
            File signingPropFile = rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                Properties signingProp = new Properties()
                signingProp.load(signingPropFile.newDataInputStream())
                resValue "string", "umeng_app_key", signingProp.get("umeng_api_key")
            } else if (System.getenv("umeng_api_key") != null) {
                resValue "string", "umeng_app_key", System.getenv("umeng_api_key")
            }
        }
        debug {
            String umengApiKey = null
            File signingPropFile = rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                Properties signingProp = new Properties()
                signingProp.load(signingPropFile.newDataInputStream())
                umengApiKey = signingProp.get("umeng_api_key")
            }
            if (umengApiKey == null) {
                umengApiKey = "0"
            }
            System.out.println("Gradle set umeng_app_key = " + umengApiKey)
            resValue "string", "umeng_app_key", umengApiKey
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Google Libraries
    compile "com.android.support:appcompat-v7:$compat_version"
    compile "com.android.support:cardview-v7:$compat_version"
    compile "com.android.support:recyclerview-v7:$compat_version"

    // IO related
    compile "com.google.code.gson:gson:2.8.0"
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.7.4'
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Image Loader
    compile 'com.squareup.picasso:picasso:2.5.2'

    // HTML Parsing
    compile 'org.jsoup:jsoup:1.8.3'

    // rx
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    // Google exo player
    compile 'com.google.android.exoplayer:exoplayer:r2.0.4'

    // Umeng stats
    compile 'com.umeng.analytics:analytics:latest.integration'

    testCompile 'junit:junit:4.12'
}
